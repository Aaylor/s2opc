/*
 *  Copyright (C) 2017 Systerel and others.
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

IMPLEMENTATION
    msg_read_request_i
   
REFINES
    msg_read_request

IMPORTS
    msg_read_request_bs

SEES
    constants,
    address_space,
    message_in_bs

CONCRETE_VARIABLES
    init_ok,
    nb_ReadValue

INITIALISATION
    init_ok := FALSE;
    nb_ReadValue := 0

OPERATIONS
    rr, nb <-- read_ReadRequest(msg) =
    BEGIN
        nb_ReadValue <-- read_req_nb_ReadValue(msg);
        init_ok := bool(0 <= nb_ReadValue &
                        nb_ReadValue <= k_n_read_resp_max);
        rr := init_ok;
        nb := nb_ReadValue
    END
    ;

    rr <-- read_nb_ReadValue =
    BEGIN
        rr := nb_ReadValue
    END
    ;

    isvalid, node, aid <-- readall_ReadValue_Node_AttributeId(msg, rvi) =
    VAR
        l_nid,
        l_nid_valid
    IN
        l_nid <-- read_req_ReadValue_NodeId     (msg, rvi);
        aid   <-- read_req_ReadValue_AttributeId(msg, rvi);
        l_nid_valid, node <-- readall_AddressSpace_Node(l_nid);
        isvalid := bool(l_nid_valid = TRUE &
                        aid /= c_AttributeId_indet)
    END

END
