/*
 *  Copyright (C) 2018 Systerel and others.
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

IMPLEMENTATION
    msg_read_request_i

REFINES
    msg_read_request

IMPORTS
    msg_read_request_bs

SEES
    constants,
    address_space,
    message_in_bs

CONCRETE_VARIABLES
    nb_ReadValue

INITIALISATION
    nb_ReadValue := 0

OPERATIONS
    p_read_ok, p_nb_ReadValue, p_tsToReturn, p_statusCode <-- check_ReadRequest(p_msg) =
    BEGIN
        p_nb_ReadValue <-- read_req_nb_ReadValue(p_msg);
        p_read_ok := bool(0              < p_nb_ReadValue &
                          p_nb_ReadValue <= k_n_read_resp_max);
        p_tsToReturn := c_TimestampsToReturn_indet;
        p_statusCode := c_StatusCode_indet;
        IF  p_read_ok = TRUE THEN
            nb_ReadValue := p_nb_ReadValue;
            p_tsToReturn <-- read_req_TimestampsToReturn(p_msg);
            IF  p_tsToReturn = c_TimestampsToReturn_indet THEN
                p_read_ok := FALSE;
                p_statusCode := e_sc_bad_timestamps_to_return_invalid
            ELSE
                /* Note: manage max age parameter for valid values but best effort valid when it is not invalid */
                p_read_ok <-- read_req_MaxAge(p_msg);
                IF  p_read_ok = FALSE THEN
                    p_statusCode := e_sc_bad_max_age_invalid
                ELSE
                    p_statusCode := e_sc_ok
                END
            END
        ELSE
            IF p_nb_ReadValue <= 0 THEN
                p_statusCode := e_sc_bad_nothing_to_do
            ELSIF p_nb_ReadValue > k_n_read_resp_max THEN
                p_statusCode := e_sc_bad_too_many_ops
            END
        END
    END
    ;

    p_isvalid, p_nid, p_aid <-- getall_ReadValue_NodeId_AttributeId(p_msg, p_rvi) =
    BEGIN
        p_isvalid, p_nid <-- getall_req_ReadValue_NodeId(p_msg, p_rvi);
        IF  p_isvalid = TRUE THEN
            p_isvalid, p_aid <-- getall_req_ReadValue_AttributeId(p_msg, p_rvi)
        ELSE
            p_aid := c_AttributeId_indet
        END
    END
    ;

    p_nb_ReadValue <-- get_nb_ReadValue =
    BEGIN
        p_nb_ReadValue := nb_ReadValue
    END

END
