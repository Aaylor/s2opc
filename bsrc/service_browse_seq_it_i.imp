/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    service_browse_seq_it_i

REFINES
    service_browse_seq_it

SEES
    constants,
    address_space

CONCRETE_VARIABLES
    nb_bvi,
    bvi_i,

    nb_bri,
    bri_i,

    Node,
    RefIndex,
    RefIndexEnd

INVARIANT
    nb_bvi : NAT &
    bvi_i  : NAT &

    nb_bri : NAT &
    bri_i  : NAT &

    Node        : t_Node_i &
    Node        : t_Node &
    RefIndex    : NAT &
    RefIndex    : t_RefIndex &
    RefIndexEnd : NAT &
    RefIndexEnd : t_RefIndex

INITIALISATION
    nb_bvi := 0;
    bvi_i  := 0;

    nb_bri := 0;
    bri_i  := 0;

    Node        := c_Node_indet;
    RefIndex    := 0;
    RefIndexEnd := 0

OPERATIONS

    p_continue <-- init_iter_browse_request(p_nb_req) =
    BEGIN
        nb_bvi     := p_nb_req;
        bvi_i      := 1;
        p_continue := bool(1 <= p_nb_req)
    END
    ;

    p_continue, p_bvi <-- continue_iter_browse_request =
    BEGIN
        p_bvi <-- get_cast_t_BrowseValue(bvi_i);
        bvi_i := bvi_i + 1;
        p_continue := bool(bvi_i <= nb_bvi)
    END
    ;

    p_continue <-- init_iter_browse_result(p_nb_bri) =
    BEGIN
        nb_bri     := p_nb_bri;
        bri_i      := 1;
        p_continue := bool(1 <= p_nb_bri)
    END
    ;

    p_continue, p_bri <-- continue_iter_browse_result =
    BEGIN
        p_bri <-- get_cast_t_BrowseResult(bri_i);
        bri_i := bri_i + 1;
        p_continue := bool(bri_i <= nb_bri)
    END
    ;

    p_continue <-- init_iter_reference(p_node) =
    BEGIN
        Node        := p_node;
        RefIndex    := 0;
        RefIndexEnd <-- get_Node_RefIndexEnd(p_node);
        p_continue := bool(RefIndexEnd >= RefIndex)
    END
    ;

    p_continue, p_ref <-- continue_iter_reference =
    BEGIN
       p_ref <-- get_RefIndex_Reference(Node, RefIndex);
       RefIndex := RefIndex + 1;
       p_continue := bool(RefIndex <= RefIndexEnd)
    END

END
