/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    browse_treatment_result_it_i

REFINES
    browse_treatment_result_it

SEES
    constants

CONCRETE_VARIABLES

    current_browseResult_idx,
    max_browseResult_idx

INVARIANT
    current_browseResult_idx : NAT &
    max_browseResult_idx : NAT &

    nb_browseResult_max_refs = max_browseResult_idx &
    current_browseResult_idx : 0..max_browseResult_idx &

    (current_browseResult_idx = 0 =>
        browseResult_to_iterate = 1..max_browseResult_idx &
        browseResult_iterated = {}
    ) &
    (current_browseResult_idx /= 0 =>
        browseResult_to_iterate = current_browseResult_idx..max_browseResult_idx &
        browseResult_iterated = 1..current_browseResult_idx)

INITIALISATION
    current_browseResult_idx := 0;
    max_browseResult_idx := 0

OPERATIONS


    p_continue <-- init_iter_browseResult(p_nb_browseResult_max_refs) =
    BEGIN
        max_browseResult_idx := p_nb_browseResult_max_refs;
        current_browseResult_idx := 0;
        p_continue := bool (0 < p_nb_browseResult_max_refs)
    END
    ;

    p_continue, p_browseResultIdx <-- continue_iter_browseResult =
    BEGIN
        current_browseResult_idx := current_browseResult_idx + 1;
        p_browseResultIdx := current_browseResult_idx;
        p_continue := bool (current_browseResult_idx < max_browseResult_idx)
    END

END